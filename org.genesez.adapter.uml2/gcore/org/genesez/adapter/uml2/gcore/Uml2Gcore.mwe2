module org.genesez.adapter.uml2.gcore.Uml2Gcore

var uml2file
var uml2slot = "uml2model"
var gcoreslot = "coremodel"

var umlsetup = org.eclipse.xtend.typesystem.uml2.Setup {
	standardUML2Setup = true
}
var gcoremm = org.eclipse.xtend.typesystem.emf.EmfMetaModel {
	metaModelPackage = "org.genesez.metamodel.gcore.GcorePackage"
}
var umlmm = org.eclipse.xtend.typesystem.uml2.UML2MetaModel {}

org.genesez.workflow.CompositeComponent {
	component = org.genesez.workflow.EmfReader {
		file = uml2file
		slot = uml2slot
	}
	component = @org.genesez.adapter.uml2.gcore.Uml2Validator {
		uml2slot = uml2slot
	}
	component = org.genesez.workflow.xpand.Model2Model {
		metaModel = umlmm
		metaModel = gcoremm
		slot = uml2slot
		outputSlot = gcoreslot
		script = "org::genesez::adapter::uml2::gcore::UML2Gcore::transform"
		globalVarDef = { name = "excludePackages" value = "'UML Standard Profile'" }
		globalVarDef = { name = "externalPackages" value = "''" }
		globalVarDef = { name = "externalStereotypes" value = "'external'" }
		globalVarDef = { name = "excludeStereotypes" value = "'exclude'" }
		globalVarDef = { name = "mapClassesInModelToExternal" value = "false" }
		globalVarDef = { name = "includeProfiles" value = "''" }
		globalVarDef = { name = "allowGeneratedXmiGuid" value = "false" }
	}
}
