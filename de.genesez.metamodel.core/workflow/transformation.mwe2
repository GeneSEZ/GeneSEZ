module de.genesez.metamodel.workflow.transformation

// properties from file (i don't know any way to reference a properties file in mwe2')
var model.dir = "model"
var ecore.file = "${model.dir}/gcore.ecore"
var modeling.dir = "modeling"
var uml2.file = "${modeling.dir}/gcore.uml"

// in future, should be something like that: http://genesez.de/metamodel.ecore
var nsUri = "platform:/plugin/de.genesez.metamodel/${ecore.file}"
// transform the root package (model) in uml model
var includedPackages = "de.genesez.metamodel"
var addNameAttribute = false
var resourcePerToplevelPackage = false

/*
 * performs the uml 2 ecore transformation
 */
Workflow {
	// initialize uml2 support
	bean = org.eclipse.xtend.typesystem.uml2.Setup {
		standardUML2Setup = true
	}
	
	// reference to the ecore meta model
	bean = org.eclipse.xtend.typesystem.emf.EmfMetaModel : emf {
		metaModelPackage = "org.eclipse.emf.ecore.EcorePackage"
	}
	
	// reference to the uml meta model
	bean = org.eclipse.xtend.typesystem.uml2.UML2MetaModel : uml { }
	
	// instantiate the uml model from file
	component = org.eclipse.xtend.typesystem.emf.XmiReader {
		modelFile = "${uml2.file}"
		outputSlot = "uml2model"
	}
	// validate uml model to be transformable into an ecore model
	component = org.eclipse.xtend.check.CheckComponent {
		metaModel = emf
		metaModel = uml
		checkFile = "org::eclipse::xtend::util::uml2ecore::constraints"
		emfAllChildrenSlot = "uml2model"
		abortOnError = true
	}
	// transform the uml model into an ecore model
	component = org.eclipse.xtend.XtendComponent {
		metaModel = emf
		metaModel = uml
		globalVarDef = { name = "nsUri" value = "'${nsUri}'" }
		globalVarDef = { name = "includedPackages" value = "'${includedPackages}'" }
		globalVarDef = { name = "addNameAttribute" value = "${addNameAttribute}" }
		globalVarDef = { name = "resourcePerToplevelPackage" value = "${resourcePerToplevelPackage}" }
		invoke = "org::eclipse::xtend::util::uml2ecore::uml2ecore::top(uml2model)"
		outputSlot = "ecoreModel"
	}
	// serialize the instantiated ecore model into a file
	component = org.eclipse.xtend.typesystem.emf.XmiWriter {
		inputSlot = "ecoreModel"
		modelFile = "${ecore.file}"
	}
}