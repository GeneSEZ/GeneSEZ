/*
 * (c) GeneSEZ Research Group
 * All rights reserved.
 *
 * Licensed according to GeneSEZ License Terms <http://www.genesez.org/en/license>
 */

import gcore;


/**
 * Validation rules for TYPO3 CMS.
 *  
 * @author	Nico Herbig <nico.herbig@fh-zwickau.de> (maintainer)
 */

// use TYPO3 CMS profile scripts
extension org::genesez::platform::typo3::cms::profile::Typo3CmsProfile;
extension org::genesez::platform::typo3::cms::profile::Typo3CmsAccessElement;

// use TYPO3 CMS specific Extbase + Fluid access element scripts
extension org::genesez::platform::typo3::cms::util::AccessElement;

// use GeneSEZ common Persistence profile scripts
extension org::genesez::metamodel::core::util::profile::GenesezPersistenceAccessElement;


// use TYPO3 CMS workflow global var scripts
extension org::genesez::platform::typo3::cms::util::GlobalVar;


// use GeneSEZ common access element scripts
extension org::genesez::metamodel::core::util::AccessElement;

// use GeneSEZ common conversion scripts
extension org::genesez::metamodel::core::util::Conversion;


// -- Extension Key + Vendor Name Checks --------------------------------------------------------------------

context MModel
	ERROR "GeneSEZ: TYPO3 CMS extension key starts with an illegal prefix! Not allowed prefixes are 'tx', 'pages', 'tt_', 'sys_', 'ts_language_' and 'csh_'! See workflow configuration: " + globalVarExtensionKey() :
	!globalVarExtensionKey().startsWith("tx") 
	&& !globalVarExtensionKey().startsWith("pages")
	&& !globalVarExtensionKey().startsWith("tt_")
	&& !globalVarExtensionKey().startsWith("sys_")
	&& !globalVarExtensionKey().startsWith("ts_language_")
	&& !globalVarExtensionKey().startsWith("csh");


context MModel
	ERROR "GeneSEZ: TYPO3 CMS extension key contains illegal characters! Allowed characters are a-z (lowercase), 0-9 and '_' (underscore)! See workflow configuration: " + globalVarExtensionKey() :
	!globalVarExtensionKey().matches("[a-z0-9_]");


context MModel
	ERROR "GeneSEZ: TYPO3 CMS extension key is to short and needs a minimum of 3 characters, not counting underscores! See workflow configuration: " + globalVarExtensionKey() :
	globalVarExtensionKey().replaceAll("_", "").length >= 3;


context MModel
	ERROR "GeneSEZ: TYPO3 CMS extension key is to long and needs a maximum of 30 characters, not counting underscores! See workflow configuration: " + globalVarExtensionKey() :
	globalVarExtensionKey().replaceAll("_", "").length <= 30;


context MModel
	WARNING "GeneSEZ: TYPO3 CMS extension key contains '_' (underscore) characters. Following TYPO3 CMS best practices the usage of underscores in extension keys should be avoided! See workflow configuration: " + globalVarExtensionKey() :
	!globalVarExtensionKey().contains("_");


context MModel
	WARNING "GeneSEZ: TYPO3 CMS extension cannot be registered as general extension in TYPO3 Extension Repository (TER) because the extension key starts with prefix 'user_'! See workflow configuration: " + globalVarExtensionKey() :
	!globalVarExtensionKey().startsWith("user_");


// -- Model Checks ------------------------------------------------------------------------------------------

/**
 * Checks if the model has has the stereotype which is needed by extension manager.
 * 
 * @return	False, if the model hasn't this sterotype, otherwise true.
 */
context MModel 
	ERROR "GeneSEZ: TYPO3 CMS extension configuration stereotype (stereotype: " + prTypo3ExtensionConfiguration() + ") is missing! See model element: " + fqmn() :
	emHasTypo3ExtensionConfigurationStereotype();
	

/**
 * Checks if the model has has a utilizable tagged value for the title of the extension which is needed by extension manager.
 * 
 * @return	False, if the model hasn't this utilizable tagged value, otherwise true.
 */
context MModel 
	if emHasTypo3ExtensionConfigurationStereotype() 
	ERROR "GeneSEZ: The title of the extension for TYPO3 CMS extension manager (stereotype: " + prTypo3ExtensionConfiguration() + ", property: " + prTypo3ExtensionConfigurationTitle() + ") is missing or empty! See model element: " + fqmn() :
	emGetTypo3ExtensionConfigurationTitle() != "";


/**
 * Checks if the model has has a utilizable tagged value for the description of the extension which is needed by extension manager.
 * 
 * @return	False, if the model hasn't this utilizable tagged value, otherwise true.
 */
context MModel 
	if emHasTypo3ExtensionConfigurationStereotype() 
	ERROR "GeneSEZ: The description of the extension for TYPO3 CMS extension manager (stereotype: " + prTypo3ExtensionConfiguration() + ", property: " + prTypo3ExtensionConfigurationDescription() + ") is missing or empty! See model element: " + fqmn() : 
	emGetTypo3ExtensionConfigurationDescription() != "";


// -- Property Checks ---------------------------------------------------------------------------------------

/**
 * Checks if the visibility of attributes from domain objects is 'protected'.
 * 
 * @return	False, if the visibility is not 'protected', otherwise true.
 */
context MAttribute 
	if ( classifier.isEntity() || classifier.isValueObject() ) && !( derived || ( static && final ) || emHasTransientStereotype() ) 
	WARNING "GeneSEZ: The visibility of the attribute from the domain object is not protected! See model element: " + fqmn() :
	visibility == "protected";

/**
 * Checks if the visibility of association roles from domain objects is 'protected'.
 * 
 * @return	False, if the visibility is not 'protected', otherwise true.
 */
context MAssociationRole 
	if ( classifier.isEntity() || classifier.isValueObject() ) && ! ( derived || emHasTransientStereotype() )
	WARNING "GeneSEZ: The visibility of the association role from the domain object is not protected! See model element: " + fqmn() :
	visibility == "protected";

/**
 * Checks if no default enumeration value is set for an attribute.
 * Hint: PHP does not support an object initialization, see http://php.net/manual/en/language.oop5.properties.php. 
 * 
 * @return	False, if the default enumeration value is set, otherwise true.
 */
context MAttribute 
	if type.isEnumeration()
	WARNING "GeneSEZ: The default enumeration value of the attribute could not be generated, because PHP does not support object initializations. Please set the default value through the constructor of this classifier or otherwise! See model element: " + fqmn() :
	!hasDefaultValue();
