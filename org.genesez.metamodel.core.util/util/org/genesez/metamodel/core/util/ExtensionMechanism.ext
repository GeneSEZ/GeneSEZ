/*
 * (c) GeneSEZ Research Group
 * All rights reserved.
 *
 * Licensed according to GeneSEZ License Terms <http://www.genesez.org/en/license>
 */

import gcore;


/**
 * Simplifies access to GeneSEZ Core meta model extension mechanism.
 *  
 * HINT: All scripts for extension mechanism are available under the prefix 'em'.
 * 
 * @author	Tobias Haubold <tobias.haubold@fh-zwickau.de> (maintainer)
 * @author	Nico Herbig <nico.herbig@fh-zwickau.de> (maintainer)
 */


// -- Stereotypes -------------------------------------------------------------------------------------------


/**
 * Checks if an element has the specified stereotype.
 *
 * @param	stereotype	The name of a stereotype.
 * @return	True if the element has the stereotype, otherwise false.
 * @api
 */
boolean emHasStereotype(MElement this, String stereotype) :
	this.stereotype.exists( s | s.name == stereotype );


/**
 * Returns a stereotype specified by its name.
 * 
 * @param	stereotype	The name of a stereotype.
 * @return	The stereotype, otherwise null if the stereotype is not assigned.
 * @api
 */
MStereotype emGetStereotype(MElement this, String stereotype) :
	this.stereotype.selectFirst( s | s.name == stereotype );


// -- Tags / Properties -------------------------------------------------------------------------------------


/**
 * Returns a tag specified by its name.
 * 
 * @param	tag	The name of a tag.
 * @return	The tag, otherwise null if the tag is not existent.
 * @api
 */
MTag emGetTag(MStereotype this, String tag) :
	property.selectFirst( p | p.name == tag );


// -- TaggedValues ------------------------------------------------------------------------------------------


/**
 * Checks if an element has one or more owning tagged values specified by its name and stereotype assignment.
 *
 * @param	stereotype	The name of a stereotype.
 * @param	tag			The name of a tagged value.
 * @return	True if the element has at least one tagged value, otherwise false.
 * @api
 */
boolean emHasOwningTaggedValues(MElement this, String stereotype, String tag) :
	taggedValue.exists( tv | tv.tag.name == tag && tv.tag.stereotype.name == stereotype ); 


/**
 * Returns the list of owning tagged values specified by its name and stereotype assignment.
 * 
 * @param	stereotype	The name of a stereotype.
 * @param	tag			The name of a tagged value.
 * @return	The list of tagged values, otherwise an empty list.
 * @api
 */
List[MTaggedValue] emOwningTaggedValues(MElement this, String stereotype, String tag) :
	taggedValue.select( tv | tv.tag.name == tag && tv.tag.stereotype.name == stereotype );
	

/**
 * Checks if an element has the specified tagged value including stereotype assignment.
 * 
 * @param	stereotype	The name of a stereotype.
 * @param	tag			The name of a tagged value.
 * @param	value		The value of a tagged value.
 * @return	True if the element has a tagged value, otherwise false.
 * @api
 */
boolean emHasTaggedValue(MElement this, String stereotype, String tag, String value) :
	taggedValue.exists( tv | tv.value == value && tv.tag.name == tag && tv.tag.stereotype.name == stereotype );


/**
 * Checks if an element has the specified tagged value reference including stereotype assignment.
 * 
 * @param	stereotype		The name of a stereotype.
 * @param	tag				The name of a tagged value.
 * @param	valueReference	The value reference of a tagged value.
 * @return	True if the element has a tagged value, otherwise false.
 * @api
 */
boolean emHasTaggedValue(MElement this, String stereotype, String tag, MElement valueReference) :
	taggedValue.exists( tv | tv.valueReference == valueReference && tv.tag.name == tag && tv.tag.stereotype.name == stereotype );


/**
 * Returns the value of a single-valued tagged value specified by its name and stereotype assignment.
 * 
 * @param	stereotype	The name of a stereotype.
 * @param	tag			The name of a tagged value.
 * @return	The value of the single-valued tagged value, otherwise an empty string, if the tagged value is not assigned.
 * @api
 */
String emGetTaggedValue(MElement this, String stereotype, String tag) :
	emHasOwningTaggedValues( stereotype, tag )
		? emOwningTaggedValues( stereotype, tag ).first().value
		: "";


/**
 * Returns the value reference of a single-valued tagged value specified by its name and stereotype assignment.
 * 
 * @param	stereotype	The name of a stereotype.
 * @param	tag			The name of a tagged value.
 * @return	The value reference of the single-valued tagged value, otherwise null, if the tagged value is not assigned.
 * @api
 */
MElement emGetTaggedValueReference(MElement this, String stereotype, String tag) :
	emHasOwningTaggedValues( stereotype, tag )
		? emOwningTaggedValues( stereotype, tag ).first().valueReference
		: null;


/**
 * Returns the boolean value of a single-valued tagged value specified by its name and stereotype assignment.
 * Please note that in both cases the return value is false, if the value of the tagged value is either false or the tagged value is not assigned.
 * 
 * @param	stereotype	The name of a stereotype.
 * @param	tag			The name of a tagged value.
 * @return	The boolean value of the single-valued tagged value, otherwise false, if the tagged value is not assigned.
 * @api
 * @todo: is this method necessary???
 */
boolean emGetTaggedBooleanValue(MElement this, String stereotype, String tag) :
	emHasTaggedValue( stereotype, tag, "true" )
		? true
		: false;


/**
 * Returns the list of values of a multi-valued tagged value specified by its name and stereotype assignment.
 * 
 * @param	stereotype	The name of a stereotype.
 * @param	tag			The name of a tagged value.
 * @return	The list of values of a multi-valued tagged value, otherwise an empty list, if the tagged value is not assigned.
 * @api
 */
List[String] emGetTaggedValues(MElement this, String stereotype, String tag) :
	emHasOwningTaggedValues( stereotype, tag )
		? emOwningTaggedValues( stereotype, tag ).value
		: {};


/**
 * Returns the list of value references of a multi-valued tagged value specified by its name and stereotype assignment.
 * 
 * @param	stereotype	The name of a stereotype.
 * @param	tag			The name of a tagged value.
 * @return	The list of value references of a multi-valued tagged value, otherwise an empty list, if the tagged value is not assigned.
 * @api
 */
List[MElement] emGetTaggedValueReferences(MElement this, String stereotype, String tag) :
	emHasOwningTaggedValues( stereotype, tag )
		? emOwningTaggedValues( stereotype, tag ).valueReference
		: {};


/**
 * Checks if a single-valued tagged value specified by its name and stereotype assignment has a utilizable value
 * which is neither empty ('') nor null ('null').
 * 
 * @param	stereotype	The name of a stereotype.
 * @param	tag			The name of a tagged value.
 * @return	True if the element has a utilizable tagged value, otherwise false.
 * @api
 */
boolean emHasUtilizableTaggedValue(MElement this, String stereotype, String tag) :
	emHasOwningTaggedValues( stereotype, tag )
		? emGetTaggedValue( stereotype, tag ) != "" && emGetTaggedValue( stereotype, tag ) != "null" 
		: false;


/**
 * Checks if a single-valued tagged value specified by its name and stereotype assignment has a utilizable value reference
 * which is not null.
 * 
 * @param	stereotype	The name of a stereotype.
 * @param	tag			The name of a tagged value.
 * @return	True if the element has a utilizable tagged value, otherwise false.
 * @api
 */
boolean emHasUtilizableTaggedValueReference(MElement this, String stereotype, String tag) :
	emHasOwningTaggedValues( stereotype, tag )
		? emGetTaggedValueReference( stereotype, tag ) != null 
		: false;
