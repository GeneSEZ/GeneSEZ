/*
 * (c) GeneSEZ Research Group
 * All rights reserved.
 *
 * Licensed according to GeneSEZ License Terms <http://www.genesez.org/en/license>
 */
import gcore;

/**
 * Provides support for the operations hashCode() and equals() based on the stereotype «java5EqualsOperation».
 * 
 * @author	andre pflueger
 * @author	tobias haubold <toh@fh-zwickau.de>
 */
extension org::genesez::metamodel::core::util::NavigateElement;
extension org::genesez::metamodel::core::util::CreateElement;
extension org::genesez::metamodel::core::util::AccessElement;

extension org::genesez::platform::java::util::JavaTypes;


/**
 * Adds the operations hashCode() and equals() operation to a class
 * if stereotype «java5EqualsOperation» is assigned and those operations are not available.
 */
MClass identity(MClass this) :
	(
		emHasStereotypeAssignment("java5EqualsOperation")
		&& (
			generalization.typeSelect(MClass).size > 0
			// [toh] i guess it should check the complete inheritance hierarchy, not only the base class
			&& !allGeneralizations().typeSelect(MClass).exists( c|c.emHasStereotypeAssignment("java5EqualsOperation") )
			//&& !generalization.typeSelect(MClass).first().emHasStereotypeAssignment("java5EqualsOperation")
		)
		&& !property.exists( p|p.emHasStereotypeAssignment("java5EqualsProperty") )
	)
		? (
			!operation.exists( o|o.name == "equals" )
				? (
					ensureOperationExistence( xmiGuid + "_equals", "equals" )
						.processOperation( "public", false, false, false )
						.ensureParameterExistence( xmiGuid + "_equals.param1", "other" )
							.processParameter( "", "in", createMultiplicity(1, 1) )
							.wireType( navModel().typeObject() )
							.operation
						.ensureReturnParameterExistence( xmiGuid + "_equals.return", "" )
							.wireType( navModel().typeBooleanPrimitive() )
				)
				: this ->
			!operation.exists( o|o.name == "hashCode" )
				? (
					ensureOperationExistence( xmiGuid + "_hashCode", "hashCode" )
						.processOperation( "public", false, false, false )
						.ensureReturnParameterExistence( xmiGuid + "_hashCode.return", "" )
							.wireType( navModel().typeIntPrimitive() )
				)
				: this
		)
		: this ->
	this;
