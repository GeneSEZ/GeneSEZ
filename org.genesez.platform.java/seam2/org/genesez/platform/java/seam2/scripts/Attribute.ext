import gcore;

extension org::genesez::platform::common::log::Log;
extension org::genesez::platform::common::AccessHelper;
extension org::genesez::platform::common::m2m::CreateBasics;
extension org::genesez::platform::common::m2m::Stereotype;
extension org::genesez::platform::java::seam2::scripts::Stereotypes;
extension org::genesez::platform::java::jbv::scripts::Stereotypes;
extension org::genesez::platform::java::seam2::scripts::Class;
extension org::genesez::platform::java::seam2::scripts::Naming;
extension org::genesez::platform::java::scripts::Naming;

/**
  *	if there is a stereotype seam2JasyptEncryption add stereotype
  *	jhvType to the attribute and remove stereotype seam2JasyptEncryption
  * (it is no longer needed, adding jhvTypeDef stereotype to class should 
  * be done before calling this method)
  */
Void checkJasyptEncryptionStereotype(MAttribute this) :
	this.hasStereotype("seam2JasyptEncryption") &&
	!this.hasStereotype("jhvType")
		? (
			this.addTypeStereotype("encryptedString", {}, {}) ->
			this.removeStereotype(this.getStereotype("seam2JasyptEncryption"))
		)
		: "";

/**
  *	adds an accessor stereotype for generating get and set methods if
  *	classifier is a seam component and the attribute does not have an 
  *	accessor stereotype (do not override modellers choices)
  *
  *	@param		this		instance of MClass
  */
Void addAccessorStereotype(MAttribute this) :
	this.classifier.hasStereotype("seam2Component")
		? (
			//add an accessor for get and set methods if no accessor 
			//stereotype exists
			!this.hasStereotype("accessor")
				? createAccessor("true", "true")
				: ""
		)
		: "";

/**
 *	checks if an attribute has the stereotype "ejb3EJB" and "seam2Injection"
 *	these stereotypes can create a conflict and "ejb3EJB" will be removed
 */
Void checkAttributesWithInjectionStereotype(MAttribute this) :
	this.hasStereotype("seam2Injection")
		? (
			this.hasStereotype("ejb3EJB")
				? this.stereotype.remove(this.stereotype.selectFirst(e|e.name == "ejb3EJB"))
				: ""
		)
		: "";

/**
  *	if the attribute has the stereotype "seam2Logger", "seam2Injection", "seam2Outjection"
  *	"seam2DataModel", "seam2Selection" or "seam2SelectionIndex" there is no need for getter/setter
  *	user defined accessor stereotypes have higher priority
  */
Void checkAccessStereotypeOfSeamStereotypedAttributes(MAttribute this) : 
	//check if the attribute has one of the named stereotypes
	this.hasStereotype("seam2Logger") ||
	this.hasStereotype("seam2Injection") ||
	this.hasStereotype("seam2Outjection") ||
	this.hasStereotype("seam2DataModel") ||
	this.hasStereotype("seam2Selection") ||
	this.hasStereotype("seam2SelectionIndex") 
		? (
			//add an accessor for no get and set methods if no accessor 
			//stereotype exists
			!this.hasStereotype("accessor")
				? createAccessor("false", "false")
				: ""
		)
		: "";

/**
  *	creates a factory method for initiation of an attribute with stereotype "seam2DataModel"
  *	if no such method exists
  */
Void addFactoryMethodForDataModelAttributes(MAttribute this) :
	this.hasStereotype("seam2DataModel") && this.hasStereotype("seam2Factory") 
		? (
			!this.hasFactoryMethodForAttribute()
				? this.classifier.addFactoryMethod(this.getTaggedValue("seam2Factory", "autoCreate"), this.getTaggedValue("seam2Factory", "scope"), !this.hasUtilizableValue("seam2Factory", "value") ? this.name : this.getTaggedValue("seam2Factory", "value"))
				: info("There is already a factory method for the attribute "+this.name.asTypeName()+" in class "+this.classifier.name.asTypeName()+". Stereotype 'seam2Factory' will be ignored.")
		)
		: "";
		

/**
  * check if there is a factory method for this attribute
  */
Boolean hasFactoryMethodForAttribute(MAttribute this) :
	this.classifier.operation.exists(e|e.hasTaggedValue("seam2Factory", "value", this.name))
		? true
		: false;


/**
  *	use attribute name for value of tagged value "value" of stereotype "seam2DataModel" if no value is available
  *	attribute name should be in plural but should not have a special plural form 
  */
Void addDataModelValue(MAttribute this) :
	/*warn(this.name.asTypeName()+":") ->
	warn("DataModel-Value (vorher): "+this.getTaggedValue("seam2DataModel", "value")) ->
	warn("Boolscher Wert: "+this.hasStereotype("seam2DataModel")+", "+!this.hasUtilizableValue("seam2DataModel", "value")+": "+(this.hasStereotype("seam2DataModel") && !this.hasUtilizableValue("seam2DataModel", "value"))) ->*/
	this.hasStereotype("seam2DataModel") && !this.hasUtilizableValue("seam2DataModel", "value") 
		? ( 
			this.setTaggedValue("seam2DataModel", "value", this.name.asTypeName())
		)
		: "" ;/*->
	warn("DataModel-Value (nachher): "+this.getTaggedValue("seam2DataModel", "value"));*/

/**
  *	use attribute name for value of tagged value "value" of stereotype "seam2Selection" if no value is available
  *	attribute name has to have the format: selected<NameOfDataModelAttributeInSingular>
  */
Void addDataModelSelectionValue(MAttribute this) : 
	this.hasStereotype("seam2Selection") && !this.hasUtilizableValue("seam2Selection", "value") 
		? this.setTaggedValue("seam2Selection", "value", this.name.asTypeName().subString(8, this.name.length).toFirstLower()+"s")
		: "";

/**
  *	use attribute name for value of tagged value "value" of stereotype "seam2SelectionIndex" if no value is available
  *	attribute name has to have the format: selected<NameOfDataModelAttributeInSingular>Index
  */
Void addDataModelSelectionIndexValue(MAttribute this) :
	this.hasStereotype("seam2SelectionIndex") && !this.hasUtilizableValue("seam2SelectionIndex", "value") 
		? this.setTaggedValue("seam2SelectionIndex", "value", this.name.asTypeName().subString(8, this.name.length-5).toFirstLower()+"s")
		: "";
	

/**
  *	if this attribute has the stereotype "jpaPersistenceContext" and the tagged value "seamPersistence" 
  *	of the stereotype "seam2Component" is true (use of Seam Managed Persistence), delete "jpaPersistenceContext"
  *	stereotype (implementation in XPAND at the moment) and add "seam2Injecion" stereotype
  *	if there is no accessor stereotype no getter and setter will be created for this attribute 
  *	
  */
Void setupSeamManagedPersistence(MAttribute this) :
	this.hasStereotype("jpaPersistenceContext") && 
	!this.classifier.hasTaggedValue("seam2Component", "seamPersistence", "false") &&
	!this.hasStereotype("seam2Injection")
	? (
		this.addInjectionStereotype("false", "true", "UNSPECIFIED", "") ->
		removeStereotype(getStereotype("jpaPersistenceContext")) ->
		!this.hasStereotype("accessor")
			? this.createAccessor("false", "false")
			: ""
	)
	: "";
	
/**
  *	if there is an attribute with name "logger" and datatype "Log" the stereotype
  *	"seam2Logger" will be added to this attribute
  */
Void checkLogger(MAttribute this) :
	(this.type.asTypeName().matches("Log") 
	|| this.type.asTypeName().matches("org.jboss.seam.log.Log")) 
	&& this.name.matches("logger") &&
	!this.hasStereotype("seam2Logger")
		? this.addLoggerStereotype("")
		: "";
		
/**
  *	if there is an attribute with name "identity" and datatype "Identity" the stereotype
  *	"seam2Injection" will be added to this attribute
  */
Void checkIdentity(MAttribute this) :
	this.type.asTypeName().matches("Identity") && this.name.matches("identity") &&
	!this.hasStereotype("seam2Injection")
		? this.addInjectionStereotype("false", "true", "UNSPECIFIED", "")
		: "";
		
/**
  *	if there is an attribute with name "facesMessages" and datatype "FacesMessages" the stereotype
  *	"seam2Injection" will be added to this attribute
  */
Void checkFacesMessages(MAttribute this) :
	this.type.asTypeName().matches("FacesMessages") && this.name.matches("facesMessages") &&
	!this.hasStereotype("seam2Injection")
		? this.addInjectionStereotype("false", "true", "UNSPECIFIED", "")
		: "";
		
/**
  *	if there is an attribute with name "events" and datatype "Events" the stereotype
  *	"seam2Injection" will be added to this attribute
  */
Void checkEvents(MAttribute this) :
	this.type.asTypeName().matches("Events") && this.name.matches("events") &&
	!this.hasStereotype("seam2Injection")
		? this.addInjectionStereotype("false", "true", "UNSPECIFIED", "")
		: "";
		
/**
  *	if there is an attribute with name "credentials" and datatype "Credentials" the stereotype
  *	"seam2Injection" will be added to this attribute
  */
Void checkCredentials(MAttribute this) :
	this.type.asTypeName().matches("Credentials") && this.name.matches("credentials") &&
	!this.hasStereotype("seam2Injection")
		? this.addInjectionStereotype("false", "true", "UNSPECIFIED", "")
		: "";