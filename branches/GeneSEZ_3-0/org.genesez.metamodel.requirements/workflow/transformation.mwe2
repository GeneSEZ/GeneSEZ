module org.genesez.metamodel.requirements

// file references
var ecore.file = "model/greq.ecore"
var uml2.file = "modeling/greq.uml"

var nsUri = "http://genesez.org/metamodel/requirements"
// transform the root package (model) in uml model
var includedPackages = "org.genesez.metamodel"
var addNameAttribute = false
var resourcePerToplevelPackage = false

// uml setup (for standalone use)
var umlsetup = org.eclipse.xtend.typesystem.uml2.Setup {
	standardUML2Setup = true
}
// reference to the ecore meta model
var emfmm = org.eclipse.xtend.typesystem.emf.EmfMetaModel {
	metaModelPackage = "org.eclipse.emf.ecore.EcorePackage"
}
// reference to the uml meta model
var umlmm = org.eclipse.xtend.typesystem.uml2.UML2MetaModel { }

Workflow {
	// instantiate the uml model from file
	component = org.eclipse.emf.mwe.utils.Reader {
		uri = "${uml2.file}"
		modelSlot = "uml2model"
	}
	// validate uml model to be transformable into an ecore model
	component = org.eclipse.xtend.check.CheckComponent {
		metaModel = emfmm
		metaModel = umlmm
		checkFile = "org::eclipse::emf::ecore::adapter::uml2::validation::UML2Constraints"
		emfAllChildrenSlot = "uml2model"
		abortOnError = true
	}
	// transform the uml model into an ecore model
	component = org.eclipse.xtend.XtendComponent {
		metaModel = emfmm
		metaModel = umlmm
		globalVarDef = { name = "nsUri" value = "'${nsUri}'" }
		globalVarDef = { name = "includedPackages" value = "'${includedPackages}'" }
		globalVarDef = { name = "addNameAttribute" value = "${addNameAttribute}" }
		globalVarDef = { name = "resourcePerToplevelPackage" value = "${resourcePerToplevelPackage}" }
		invoke = "org::eclipse::emf::ecore::adapter::uml2::UML2Ecore::top(uml2model)"
		outputSlot = "ecoreModel"
	}
	/*
		this serializer sets the uri into the ecore model references
		due to the base path of this workflow is not the ecore file location, the model references are wrong!
	<!--component id="serialize" class="org.eclipse.emf.mwe.utils.Writer">
		<modelSlot value="ecoreModel" />
		<uri value="${ecore.file}" />
	</component-->
	*/
	// serialize the instantiated ecore model into a file
	component = org.eclipse.xtend.typesystem.emf.XmiWriter {
		inputSlot = "ecoreModel"
		modelFile = "${ecore.file}"
	}
}
