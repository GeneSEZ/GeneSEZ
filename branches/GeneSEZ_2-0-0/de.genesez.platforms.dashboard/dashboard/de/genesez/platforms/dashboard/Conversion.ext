import gcore;

/**
 * Provides function to convert model information into more representable information.
 * @author tobias haubold
 */

// - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -

/**
 * calculates a difference between the first and the second parameter if possible and returns it, otherwise
 * returns the third parameter
 */
String diff(String before, String after, String instead) :
	before.isDate() && after.isDate()
	? daydiff(before, after)
	: ( before.isNumber() && after.isNumber()
		? after.asInteger() - before.asInteger()
		: instead
	);

Integer daydiff(String before, String after) :
	before.isDate() && after.isDate()
	? diffInDays(before, after)
	: 0;

boolean isDate(String this) : JAVA de.genesez.platforms.dashboard.Conversion.isDate(java.lang.String);
boolean isNumber(String this) : JAVA de.genesez.platforms.dashboard.Conversion.isNumber(java.lang.String);
private Integer diffInDays(String before, String after) : JAVA de.genesez.platforms.dashboard.Conversion.daydiff(java.lang.String, java.lang.String);

Integer percentage(Integer total, Integer part) :
	total == 0
	? -1
	: (part.asDouble() / total.asDouble() * 100).round();

Integer inverse(Integer this) : this == -1 ? -1 : 100 - this;

Real average(List[Integer] this) : JAVA de.genesez.platforms.dashboard.Conversion.average(java.util.List);

Integer round(Real this) : JAVA de.genesez.platforms.dashboard.Conversion.round(java.lang.Double);
Real asDouble(Integer this) : toString().asDouble();
Real asDouble(String this) : JAVA java.lang.Double.valueOf(java.lang.String);

/**
 * getter for the path from a package back to the model, assuming every package is one directory
 * @return	the path back ending with a slash '/'
 */
String pathBack(MPackage this) : pathBack("");

/**
 * getter for the path from a package back to the model, assuming every package is one directory
 * @return	the path back ending with a slash '/' and the 'path' parameter
 */
String pathBack(MPackage this, String path) :
	metaType == MModel
	? path
	: nestingPackage.pathBack("../" + path);
