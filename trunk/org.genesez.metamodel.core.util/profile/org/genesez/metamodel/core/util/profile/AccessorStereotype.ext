/*
 * (c) GeneSEZ Research Group
 * All rights reserved.
 *
 * Licensed according to GeneSEZ License Terms <http://www.genesez.org/en/license>
 */
import gcore;

/**
 * Provides utility functions for the GeneSEZ uml profile.
 * 
 * @author	tobias haubold <toh@fh-zwickau.de> (maintainer)
 */
extension org::genesez::metamodel::core::util::AccessHelper;
extension org::genesez::metamodel::core::util::ExtensionMechanism;
extension org::genesez::metamodel::core::util::m2m::CreateElement;
extension org::genesez::metamodel::core::util::profile::GeneSEZProfile;
extension org::genesez::util::logging::Logging;


/**
 * Assigns the «accessor» stereotype and creates the specified tagged values.
 * 
 * @return	The property owning the tagged values.
 */
MProperty prAssignGeneSezAccessorStereotype(MProperty this, boolean get, boolean set, boolean add, boolean remove) :
	prDoAssignGeneSezAccessorStereotype( get, set, add, remove );

/**
 * Assigns the «accessor» stereotype and creates the specified tagged values.
 * 
 * @return	The classifier owning the tagged values.
 */
MClassifier prAssignGeneSezAccessorStereotype(MClassifier this, boolean get, boolean set, boolean add, boolean remove) :
	prDoAssignGeneSezAccessorStereotype( get, set, add, remove );

/**
 * Assigns the specified values to the tags of the «accessor» stereotype.
 * 
 * @return	The element owning the tagged values.
 */
private MElement prDoAssignGeneSezAccessorStereotype(MElement this, boolean get, boolean set, boolean add, boolean remove) :
	ensureTaggedValueExistence(
		model().prEnsureGeneSezAccessorStereotype().property.selectFirst( p|p.name == prAccessorGetter() ), get.toString()
	).owningElement
	.ensureTaggedValueExistence(
		model().prEnsureGeneSezAccessorStereotype().property.selectFirst( p|p.name == prAccessorSetter() ), set.toString()
	).owningElement
	.ensureTaggedValueExistence(
		model().prEnsureGeneSezAccessorStereotype().property.selectFirst( p|p.name == prAccessorAdd() ), add.toString()
	).owningElement
	.ensureTaggedValueExistence(
		model().prEnsureGeneSezAccessorStereotype().property.selectFirst( p|p.name == prAccessorRemove() ), remove.toString()
	).owningElement;

/**
 * Returns the «accessor» stereotype of the GeneSEZ profile with all of its tags.
 * It checks if the stereotype already exists and creates it if needed.
 */
MStereotype prEnsureGeneSezAccessorStereotype(MModel this) :
	anyStereotype.exists( s|s.name == prAccessor() )
		? anyStereotype.selectFirst( s|s.name == prAccessor() ).prEnsureGeneSezAccessorTags()
		: prEnsureGeneSezAccessorTags(
			ensureStereotypeExistence( accessorXmiGuid(), prAccessor() )
		);

/**
 * Ensures the existence of all tags of the «accessor» stereotype.
 * 
 * @return	The «accessor» stereotype.
 */
private MStereotype prEnsureGeneSezAccessorTags(MStereotype this) :
	ensureTagExistence( prAccessorGetter(), "Boolean" ).stereotype
		.ensureTagExistence( prAccessorSetter(), "Boolean" ).stereotype
		.ensureTagExistence( prAccessorAdd(), "Boolean" ).stereotype
		.ensureTagExistence( prAccessorRemove(), "Boolean" ).stereotype;

/**
 * Returns the xmi guid of the «accessor» stereotype.
 */
private String accessorXmiGuid() : "org.genesez.adapter.uml2.profile.accessor";
