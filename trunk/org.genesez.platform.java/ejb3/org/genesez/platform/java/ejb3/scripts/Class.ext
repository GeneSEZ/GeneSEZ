import gcore;

extension org::genesez::util::logging::Logging;
extension org::genesez::metamodel::core::util::AccessElement;
extension org::genesez::metamodel::core::util::CreateElement;
extension org::genesez::metamodel::core::util::NavigateElement;
extension org::genesez::platform::java::util::Naming;
extension org::genesez::platform::java::util::JavaTypes;
extension org::genesez::platform::java::java5::scripts::Class;
extension org::genesez::platform::java::ejb3::scripts::Stereotypes;
extension org::genesez::platform::common::m2m::MakeElement;
extension org::genesez::platform::common::m2m::RemoveElement;
extension org::genesez::metamodel::core::util::profile::AccessorStereotype;
/* remove this import to be independent from jpa cartridge */
extension org::genesez::platform::java::jpa::scripts::Stereotypes;

/**
 *	transform Jacobson stereotype "control" to a stateless session bean 
 *	with local business interface
 *	transform Jacobson stereotype "boundary" to a stateless session bean 
 *	with remote business interface
 */
Void transformJacobsonStereotypes(MClass this) :
	this.emHasStereotypeAssignment("control")
		? (
			this.addSessionBeanStereotype(this.name, "", "", false, "EntityManager", "None") ->
			this.addLocalStereotype("")
		)
		: "" ->
	this.emHasStereotypeAssignment("boundary")
		? (
			this.addSessionBeanStereotype(this.name, "", "", false, "EntityManager", "None") ->
			this.addRemoteStereotype("")
		)
		: "" ;

/**
 *	transform WAM stereotype "tool" to a stateless session bean with local business interface
 *	transform WAM stereotype "automaton" to a stateless session bean with local business interface
 *	and an timer operation
 */
Void transformWAMStereotypes(MClass this) :
	this.emHasStereotypeAssignment("tool")
		? (
			this.addSessionBeanStereotype(this.name, "", "", false, "EntityManager", "None") ->
			this.addLocalStereotype("")
		)
		: "" ->
	this.emHasStereotypeAssignment("automaton")
		? (
			this.addSessionBeanStereotype(this.name, "", "", false, "EntityManager", "None") ->
			this.addLocalStereotype("") ->
			this.makeAttribute("timerService", "TimerService", "public") ->
			(
				this.ensureOperationExistence( this.xmiGuid + ".executeTask", "executeTask" )
					.processOperation( "public", false, false, false )
					.ensureParameterExistence( this.xmiGuid + ".executeTask.timer", "timer" )
						.processParameter( "", "in", createMultiplicity(0, 1) )
						.wireType( this.navModel().typeTimer() )
//	old impl:
//				let op = this.makeOperation("executeTask", "public", false, false, false) :				
//					op.parameter.add(op.makeParameter("timer", "", "in", this.navModel().typeTimer) )
			)
		)
		: "" ;

/**
 *	adds the stereotype "ejb3Remote" to a session bean
 *	if the stereotype "jwsWebService" exists and 
 *	there is neither stereotype "ejb3Local" nor "ejb3Remote"
 *	available
 *
 *	this		instance of MClassifier	
 */
Void addRemoteBusinessInterface(MClassifier this) :
	emHasStereotypeAssignment("ejb3SessionBean") &&
	emHasStereotypeAssignment("jwsWebService") &&
	realization.isEmpty && 
	!(emHasStereotypeAssignment("ejb3Local") || emHasStereotypeAssignment("ejb3Remote"))
		? addRemoteStereotype("")
		: "";

/**
 *	adds the interface "MessageListener" to a message driven bean
 *	if there is no such realization
 *	adds the operation "onMessage(Message)" to a message driven bean
 *	if there is no such operation
 *
 *	@param		this		instance of MClass
 */
MClassifier addMessageListener(MClass this) :
	emHasStereotypeAssignment("ejb3MessageDrivenBean")
		? ( 
			!realization.exists(e|e.name().matches("MessageListener") || e.name().matches("javax.jms.MessageListener"))
				? realization.add( this.navModel().typeMessageListener() )
				: "" ->
			!this.operation.exists(e|e.name.matches("onMessage"))
				? (
					ensureOperationExistence( xmiGuid + ".onMessage", "onMessage" )
						.processOperation( "public", false, false, false )
						.ensureParameterExistence( xmiGuid + ".onMessage.message", "message" )
							.processParameter( "", "in", createMultiplicity(0, 1) )
							.wireType( this.navModel().typeMessage() )
//	old impl:
//					let o = makeOperation("onMessage", "public") :
//						o.parameter.add(o.makeParameter("message", "in", this.navModel().typeMessage() ))
				)
				: ""
		)
		: "";

/**
 *	adds the stereotype "ejb3Local" to a session bean
 *	if no business interface is implemented and 
 *	there is neither stereotype "ejb3Local" nor "ejb3Remote"
 *	available
 *
 *	this		instance of MClassifier	
 */
addLocalBusinessInterface(MClassifier this) :
	emHasStereotypeAssignment("ejb3SessionBean") &&
	realization.isEmpty && 
	!(emHasStereotypeAssignment("ejb3Local") || emHasStereotypeAssignment("ejb3Remote"))
		? addLocalStereotype("")
		: "";

/**
  * adds no argument constructor to a session or message 
  *	driven bean if there is not already one
  */	
MClassifier addStandardConstructor(MClass this) :
	(this.emHasStereotypeAssignment("ejb3SessionBean") || this.emHasStereotypeAssignment("ejb3MessageDrivenBean")) &&
	!(operation.exists(e|e.name == name && e.parameter.isEmpty))
		? makeStandardConstructor("public")
		: "";
	
/**
  *	evaluates "ejb3SessionBean" stereotype and adds entityManager attribute
  *	for jpa uasage in the session bean.
  *
  *	this operation binds the ejb3 cartridge to jpa cartridge because the attributes
  *	have to be annotated. It is preferred to have DRY code instead of no coupling and 
  *	it is preferred because in most cases ejb3 and jpa are used together
  *
  **/
Void checkUseOfEntityManager(MClass this) :
	!emHasTaggedValue("ejb3SessionBean", "entityManager", "None")
		? (	emHasTaggedValue("ejb3SessionBean", "entityManager", "EntityManager")
			? (	addEntityManager("EntityManager") ->
				addStereotypesForEntityManager("entityManager", "[]")
			)
			:(
			emHasTaggedValue("ejb3SessionBean", "entityManager", "EntityManager_Extended")
				? (	addEntityManager("EntityManager_Extended") ->
					this.emHasTaggedValue("ejb3SessionBean", "stateful", "true")
						? addStereotypesForEntityManager("entityManager", "[EXTENDED]")
						: (
							addStereotypesForEntityManager("entityManager", "[]") ->
							logError("Use of an EntityManager with extended persistence context in a stateless SessionBean "+this.name+". Context was switched from extended to normal.")
						)

				)
				:(
				emHasTaggedValue("ejb3SessionBean", "entityManager", "Session")
					? (	addEntityManager("Session") ->
						addStereotypesForEntityManager("session", "[]")
					)
					: (
					emHasTaggedValue("ejb3SessionBean", "entityManager", "Session_Extended")
						? (	addEntityManager("Session_Extended") ->
							this.emHasTaggedValue("ejb3SessionBean", "stateful", "true")
								? addStereotypesForEntityManager("session", "[EXTENDED]")
								: (
									addStereotypesForEntityManager("session", "[]") ->
									logError("Use of an SessionManager with extended persistence context in the stateless SessionBean "+this.name+". Context was switched from extended to normal.")
								)
							
						)
						: ""
					)
				)
			)
		)
		: "";
	
/**
  *	adds "ejb3PersistenceContext" and "accessor" to the attribute with the given name
  *
  *	@param	attributeName	attribute name of the entity manager
  *	@param	fetchTypes		fetch types for the persistenceContext stereotype
  */
Void addStereotypesForEntityManager(MClass this, String attributeName, String fetchTypes) :
	let e = this.property.typeSelect(MAttribute).selectFirst(e|e.name == attributeName) :
		e != null
			? (	addPersistenceContextStereotype(e, fetchTypes, "[]", "[]") ->
				e.createAccessorForEntityManager()
			)
			: "";
	
Void createAccessorForEntityManager(MProperty this) :
	!this.classifier.emHasTaggedValue("ejb3SessionBean", "entityManagerAccess", "None")
		? (	this.classifier.emHasTaggedValue("ejb3SessionBean", "entityManagerAccess", "Both")
			? emAssignGeneSezAccessorStereotype(this, true, true, false, false)
			: (	this.classifier.emHasTaggedValue("ejb3SessionBean", "entityManagerAccess", "Getter")
				? emAssignGeneSezAccessorStereotype(this, true, false, false, false)
				: (	this.classifier.emHasTaggedValue("ejb3SessionBean", "entityManagerAccess", "Setter")
					? emAssignGeneSezAccessorStereotype(this, false, true, false, false)
					: ""
				) 
			) 
		)
		: emAssignGeneSezAccessorStereotype(this, false, false, false, false) ->
	"";

/**
  *	adds an attribute with name "entityManager" or "session" if an attribute
  *	with the datatype "EntityManager" or "Session" not already exists
  */
Void addEntityManager(MClass this, String type) :
	!existEntityManager() 
		? (type.matches("EntityManager")
			? makeAttribute("entityManager", "EntityManager", "private")
			: (type.matches("EntityManager_Extended")
				? makeAttribute("entityManager", "EntityManager", "private")
				: (type.matches("Session")
					? makeAttribute("session", "Session", "private")
					: (type.matches("Session_Extended")
						? makeAttribute("session", "Session", "private")
						: ""
					)	
				)
			)
		)
		: "" ->
	"";

/**
  *	checks if class contains an attribute with the type "EntityManager" or "Session"
  */
boolean existEntityManager(MClass this) : 
	this.property.exists(e|e.type.name().matches("EntityManager")) || this.property.exists(e|e.type.name().matches("Session"))
		? true
		: false;		