/**
 * Conversion functions to display full qualified names of model elements
 * 
 * @author	tobias haubold
 * @date	2008-07-17
 */
import uml;

extension org::genesez::util::logging::Logging;

/**
 * Checks if the operation is a constructor or not.
 * @return true if its a constructor, otherwise false
 */
Boolean isConstructor(Operation this) :
	getAppliedStereotypes().exists(e|e.name == "Create" || e.name == "create");

/**
 * Checks if the operation is a destructor or not.
 * @return true if its a destructor, otherwise false
 */
Boolean isDestructor(Operation this) :
	getAppliedStereotypes().exists(e|e.name == "Destroy" || e.name == "destroy");

/**
 * Checks if the parameter is a return parameter.
 * @return true if its a return parameter, otherwise false
 */
Boolean isReturnParameter(Parameter this) :
	direction.toString() == "return";


/**
 * Logs a warning if the specified uml profile couldn't be resolved.
 * @return	the uml profile itself
 */
Profile logUnresolvedUmlProfile(Profile p) :
	p.isProxy()
		? logWarn( p, "Unresolved UML Profile: " )
		: p;

/**
 * Returns true if the specified instance is a proxy object, i.e. an unresolved object, otherwise false.
 */
boolean isProxy(Element this) : 
	JAVA org.genesez.adapter.uml2.TransformationHelper.isProxy(org.eclipse.uml2.uml.Element);


/**
 * calculates the full qualfied name of a model
 * @return	full qualified name
 */
String fqn(Model this) : name;

/**
 * calculates the full qualfied name of a package, i.e. all package names concatenated by :: without the name of the model element
 * @return	full qualified name
 */
String fqn(Package this) : nestingPackage != null && nestingPackage.metaType != Model ? nestingPackage.fqn() + "::" + name : name;

/**
 * calculates the full qualfied name of a classifier
 * @return	full qualified name
 */
String fqn(Classifier this) : owner.fqn() + "::" + name;

/**
 * calculates the full qualfied name of an operation
 * @return	full qualified name
 */
String fqn(Operation this) : owner.fqn() + "::" + name + "(...)";

/**
 * calculates the full qualfied name of a parameter
 * @return	full qualified name
 */
String fqn(Parameter this) : owner.fqn() + "::" + name;

/**
 * calculates the full qualfied name of a property
 * @return	full qualified name
 */
String fqn(Property this) : owner.fqn() + "::" + name;


/**
 * calculates the full qualfied name of a named element
 * @return	full qualified name
 */
String fqn(NamedElement this) : owner != null ? owner.fqn() + "::" + name : name;

/**
 * calculates the full qualfied name of a named element
 * @return	full qualified name
 */
String fqn(Element this) : owner != null ? owner.fqn() + "::" + toString() : toString();

/**
 * Returns a qualified model name for identification of the specified model element.
 */
String fqmn(Element this) : fqn() + " [" + metaType + " | " + xmiId() + "]";

/**
 * getter for the xmiId of an uml element
 * @param	obj	an uml object
 * @return	the unique xmi id
 */
String xmiId (Element obj) : 
	JAVA org.genesez.adapter.uml2.TransformationHelper.getXmiId(org.eclipse.emf.ecore.EObject);
